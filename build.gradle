plugins {
    id 'java'
    id 'maven'

    id 'idea'
    id 'eclipse'

    id 'org.spongepowered.plugin' version '0.9.0'
    id 'net.minecrell.licenser' version '0.4.1'
}

defaultTasks 'clean', 'build'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

group = 'net.caseif.lightbox'
version = '1.0.0'

ext {
    projectName = 'lightbox'
    inceptionYear = '2019'
    packaging = 'jar'
    description = 'Simple configuration loader for Sponge plugins.'

    url = 'https://github.com/caseif/LightBox'
    scmUrl = 'github.com/caseif/LightBox.git'
}

configurations {
    deployerJars
}

repositories {
    mavenCentral()
    maven {
        name 'sponge'
        url 'http://repo.spongepowered.org/maven'
    }
}

dependencies {
    compile 'org.spongepowered:spongeapi:7.1.0-SNAPSHOT'

    deployerJars 'org.apache.maven.wagon:wagon-ssh:2.9'
}

license {
    header getRootProject().file('HEADER.txt')
    include '**/*.java'
    ignoreFailures false

    ext {
        year = '2019'
        author = 'Max Roncace'
        email = 'mproncace@gmail.com'
    }
}

// Read source files using UTF-8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar.manifest.mainAttributes(
        'Implementation-Title': name,
        'Implementation-Version': version
)

artifacts {
    archives jar
}

wrapper {
    gradleVersion = '5.2.1'
}

uploadArchives {
    repositories.mavenDeployer {
        //setUniqueVersion(true)
        configuration = configurations.deployerJars
        if (project.hasProperty('mavenRepo')) {
            repository(url: project.property('mavenRepo')) {
                if (project.hasProperty('mavenPrivKey')) {
                    authentication(userName: project.property('mavenUser'), privateKey: project.property('mavenPrivKey'))
                } else {
                    authentication(userName: project.property('mavenUser'), password: project.property('mavenPass'))
                }
            }
        }

        pom {
            groupId = project.group
            version = project.version
            artifactId = project.ext.projectName
            project {
                packaging 'jar'
                description project.ext.description
                url project.ext.url

                scm {
                    url 'https://' + project.ext.scmUrl
                    connection 'scm:git:git://' + project.ext.scmUrl
                    developerConnection 'scm:git:git@' + project.ext.scmUrl
                }

                licenses {
                    license {
                        name 'MIT Clause'
                        url 'https://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}
